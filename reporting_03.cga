/**
 * File:    reporting_03.cga
 * Created: 15 Nov 2011 17:44:12 GMT
 * Author:  andi
 */

version "2012.1"  


# ---------------------------------------
# Attributes
# ---------------------------------------


@Group("City Attributes",0)
attr greenspacePercentage = 30
@Group("City Attributes")
attr mixedOffice = 0.2  			# 20% of mixed use buildings is office
	

@Group("Building attributes", 1) @Range("Office","Mixed","Residential")
attr landuseType = 
	33% : "Mixed"
	33% : "Office"
	else : "Residential"


@Group("Building attributes")
attr baseFloors = 
	case landuseType == "Residential" : 0 
	else : ceil(rand(0,3))

@Group("Building attributes", 1)
attr nFloor = ceil(rand(1,15)  *buildingHeightFactor)

@Group("Building attributes")
attr floorHeight = rand(4,5)



	


@Group("Footprint",3)  @Range(3,6)
attr distanceStreet = 
	20% : 0
	else : rand(3,6)

@Group("Footprint")  @Range(4,8)
attr distanceBuildings = 
	50% : 0
	else : rand(4,8)




@Group("Viz",4)
@Range("massOnly", "floors", "massAndFloors")
attr vizMode = "massAndFloors"


@Group("Mapped",99)
attr buildingHeightFactor = 1




# ---------------------------------------
# Start with the lot
# ---------------------------------------

@Hidden
attr plotArea = 0 # used to calc FAR

@StartRule
Lot --> 
	case p(greenspacePercentage/100):
		report("Area.Greenspace",geometry.area)
		GreenSpace
	else:
		set(plotArea, geometry.area)
		report("Area.BuildUp",geometry.area)
		BuildingLot


LotInner --> Lot
LotCorner --> Lot


# ---------------------------------------
# BuildingLot
# ---------------------------------------

BuildingLot -->
	report("Floor Height", floorHeight)
	report("Floor Count", nFloor)
	setback(distanceStreet)
		{ streetSide: OpenSpace 
		| remainder: Parcel }


Parcel -->
	setback(distanceBuildings/2)
		{ noStreetSide: OpenSpace 
		| remainder: Footprint }	

# ---------------------------------------
# Mixed
# ---------------------------------------

Footprint -->
	[ extrude(baseFloors*floorHeight) RetailBase ]
	t(0,baseFloors*floorHeight,0) 
	extrude(floorHeight*(nFloor-baseFloors)) Mass

RetailBase --> 
	MassViz
	split(y){~floorHeight : Floor("Retail") }*




Residential --> extrude(floorHeight*(nFloor-baseFloors)) Mass


# -----------------------
# Mass / Floors
# -----------------------

Mass -->
	MassViz
	split(y){ floorHeight: Floor(landuseType) }*

Floor(type) -->	comp(f){ bottom: reverseNormals FloorBottom(type) }


	
FloorBottom(type) -->
	case type == "Retail":
		report("GFA.Retail",geometry.area) 
	 	report("FAR",geometry.area/plotArea)
	 	color("#ff4444")
	 	FloorViz
	 	
	case type == "Office" || (type == "Mixed" && split.index < mixedOffice*split.total): 
		report("GFA.Office",geometry.area) 
	 	report("FAR",geometry.area/plotArea)
	 	color("#44ff44")
	 	FloorViz
	 		
	else:
		report("GFA.Residential",geometry.area) 
	 	report("FAR",geometry.area/plotArea)
	 	color("#4444ff") 
	 	FloorViz


# -----------------------
# Leaf Shapes 
# -----------------------

MassViz -->
	case vizMode == "massOnly" :
		Mass.
	case vizMode == "massAndFloors" :
		set(material.color.a,0.2) Mass.
	else:
		NIL

FloorViz -->
	case vizMode == "massOnly" :
		NIL
	else:
		FloorViz.



GreenSpace --> color("#22aa22") GreenSpace.
		
OpenSpace --> color("#777777")

Ground --> color("#777777")

		

