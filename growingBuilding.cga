/**
 * File:    growingBuilding.cga
 * Created: 3 Nov 2008 15:31:30 GMT
 * Author:  andi
 */
 
version "2014.0" 


import brickroof : "brickroof.cga" 

/* Attributes *************************************/
@Group("Script-Controlled",0)
attr height 			= 24
@Group("Script-Controlled")
attr width 				= 10

attr groundfloor_height = 5.5
attr floor_height 		= 4.5
attr tile_width 		= 3
@Color
attr wallColor          = "#ffffff"
attr window_width		= 1.4
attr door_width			= 2
attr length 			= 10
attr brick_w 			= 0.4

/* Assets *************************************/

# Geometries
const window_asset 			= "facades/elem.window.frame.obj"
const round_wintop_asset 		= "facades/round_windowtop.obj"
const tri_wintop_asset 		= "facades/triangle_windowtop.obj"
const halfarc_asset 			= "facades/arc_thin.obj"
const ledge_asset 			= "facades/ledge.03.twopart_lessprojection.obj"
const modillion_asset 		= "facades/ledge_modillion.03.for_cornice_ledge_closed.lod0.obj"

# Textures
const wall_tex				= "facades/textures/brickwall2.jpg"
const wall2_tex				= "facades/textures/brickwall2_bright.jpg"
const dirt_tex				= "facades/textures/dirtmap.15.jpg"
const doortop_tex				= "facades/textures/doortoptex.jpg"


/* Functions *************************************/

# this function will return the requested texture string 
# of the type facade/window.x.jpg
getWindowTex(inst) = "facades/textures/window." + inst + ".jpg"

getDoorTex(inst) = "facades/textures/doortex." + inst + ".jpg"
		
const wall_inset = 0.4




/* Initial Shape starting rule *************/



# extrude the lot to building height
@StartRule
Lot --> 
	set(material.specular.r,0)
	set(material.specular.g,0)
	set(material.specular.b,0)
	
	s(length,height,width) 
	primitiveCube()
	Building



# we only need the front face for this example
//Building --> comp(f) { front : FrontfacadeTex | side : Check }
Building --> comp(f) { front : FrontfacadeTex | side : CheckFacade | top : CheckRoof }


CheckRoof -->
	case width > 1.9 : brickroof.Roof
	else : NIL

CheckFacade -->
	case width > 1 : FrontfacadeTex
	else : NIL


FrontfacadeTex -->
	setupProjection(0, scope.xy, 2.25, 1.5, 1) 		// setup 2.25m x 1.5m texture tiles (and distortion in z)
	setupProjection(2, scope.xy, scope.sx, scope.sy) 	// setup texture channel 2 for dirt mapping over whole facade
	Frontfacade
	
# the front facade is subdivided into one front groundfloor 
# and upper floors
Frontfacade -->	
	setupProjection(0, scope.xy, 2.25, 1.5, 1) 		// setup 2.25m x 1.5m texture tiles (and distortion in z)
	setupProjection(2, scope.xy, scope.sx, scope.sy) 	// setup texture channel 2 for dirt mapping over whole facade
		split(y){ groundfloor_height    : Floor(split.index) 	
		| floor_height : Floor(split.index)   		 			
		| floor_height : Floor(split.index)		 		
		| {~floor_height : Floor(split.index)}*			
		| floor_height : Floor(999)						
		| 0.5 : s('1,'1,0.3) LedgeAsset}  



# depending on the floor index, floors are split into bottom ledge, tile (window) and top ledge area
Floor(floorindex) -->
	case floorindex == 0 : 
		// Groundfloor has tiles only, no ledges
		Subfloor(floorindex)
	case floorindex == 2: 
		// because windows start at floor level, no bottom ledge for second floor
		// but balcony instead 
		split(y){~1 : Subfloor(floorindex) Balcony | 0.5 : TopLedge}
	else : 
		// all other floors have bottom ledge, tile and top ledge area
		split(y){1 : BottomLedge(floorindex) | ~1 : Subfloor(floorindex) | 0.5 : TopLedge}
	







# ----------------------------------------
# Tiles
# ----------------------------------------

# Tiles consist of small wall areas on left and right edges and repeating tiles in between
Subfloor(floorindex) -->
	split(x){ 0.5 : Wall(1) 
			| { ~tile_width : Tile(floorindex) }* 
			| 0.5 : Wall(1) }
	

# a tile consists of a centered window element and 
# wall elements left and right.		
Tile(floorindex) -->
	case floorindex == 0 :
		// the groundfloor has different dimensions and different shapes 
		split(x){ ~1 : SolidWall 
			|  door_width : DoorTile
			| ~1 : SolidWall }		 
	else : 	
		split(x){ ~1 : Wall(getStyle(floorindex))
			|  window_width : WindowTile(floorindex)
			| ~1 : Wall(getStyle(floorindex)) }		 
	





	




# ----------------------------------------
# Windows
# ----------------------------------------

# different window ornaments for windowtiles on different floors
WindowTile(floorindex) -->
	case floorindex == 1 || floorindex == 999 || comp.sel == "side": Window
	case floorindex == 2 : Window t(0,'1,0) WindowOrnamentRound
	else : Window WindowLedge t(0,'1,0) WindowOrnamentTriangle
	

# set dimensions for the triangle window element and insert it
WindowOrnamentTriangle --> 
	s('1.7, 1.2, 0.3) center(x) i(tri_wintop_asset) Wall(0)

# set dimensions for the triangle window element and insert it
WindowOrnamentRound -->
		s('1.7, 1.2, 0.4) center(x) i(round_wintop_asset) Wall(0)
		split(x){~1 : WindowMod | window_width : NIL | ~1 : WindowMod }

	
WindowMod -->
// the modillion asset is scaled in negative y direction 
// which aligns its top at the window top edge
	s(0.2,'1.3,'0.6) t(0,'-1,0) center(x) i(modillion_asset) Wall(0)



WindowLedge -->
	s('1.5, 0.2, 0.1) t(0,-0.2,0) center(x)   primitiveCube() Wall(0)
	

	
	
# firstly, the depth and the depth position of the future window is set
# secondly, one of nine window textures is randomly selected	  
# finally, the window geometry asset is inserted
Window -->
	s('1,'1,0.2) t(0,0,-0.2)
	t(0,0,0.02)
	[ i(window_asset) Wall(0) ]
	Glass
	
Glass -->
	setupProjection(0,scope.xy, scope.sx, scope.sy)
	set(material.colormap, getWindowTex(ceil(rand(7)))) 
	projectUV(0)

# ----------------------------------------
# Door
# ----------------------------------------

# The door tile is split vertically into door, arc and top area
DoorTile -->
	// to ensure non-elliptic arcs, the height of the arc area need to
	// be half the width of the door (the current x scope)
	split(y){~1 : Door | scope.sx/2 : Arcs | 0.5 : Arctop}

# Adds wall material and a centered modillion
Arctop -->
	Wall(1)
	s(0.5,'1,0.3) center(x) i(modillion_asset) Wall(1)

# 
Arcs -->  
	s('1,'1,wall_inset) t(0,0,-wall_inset)
	Doortop primitiveCube()
	split(x){ ~1 : ArcAsset 
			| ~1 : r(0,0,-90) t('-1,0,0) ArcAsset}

Doortop -->
	setupProjection(0,scope.xy, scope.sx, scope.sy)
	set(material.colormap, doortop_tex) 
	projectUV(0)
	
# inserts the actual arc asset
ArcAsset --> i(halfarc_asset) Wall(1)

# and geometry insert
# TODO: fix door uv bug (problem with uv handling on split?)
Door -->
	//s('1,'1,0.1)
	t(0,0,-wall_inset)
	setupProjection(0,scope.xy, scope.sx*2, scope.sy)
	set(material.colormap, getDoorTex(ceil(rand(3)))) 
	projectUV(0)
	





# ----------------------------------------
# Ledges
# ----------------------------------------


TopLedge --> WallStripe 

BottomLedge(floorindex) -->

	case floorindex == 1 : split(y){~1 : Wall(0) | ~1 : s('1,'1,0.2) LedgeAsset}
	case floorindex == 999 : split(y){~1 : WallStripe | ~1 : s('1,'1,0.2) LedgeAsset}
	else : WallStripe

WallStripe --> split(x){ 0.5 : Wall(1) | ~1 : Wall(2) | 0.5 : Wall(1) } 


LedgeAsset --> i(ledge_asset) Wall(0)



# ----------------------------------------
# Balcony
# ----------------------------------------

Balcony --> 
	s('1,2,1) t(0,-0.3,0) primitiveCube() 
	split(y){0.2 : BalconyBeams 
			| 0.3 : BalconyFloor 
			| 1 : RailingBox }


# The beams supporting the balcony are created with a repeating split
BalconyBeams -->			
	split(x){ ~0.4 : s(0.2,'1,'0.9) center(x)  Wall(0) }*

# Get the front, left and right components (faces) of the RailingBox shape
RailingBox -->
	comp(f){front : Rail | left : Rail | right : Rail} // | left : Rail | right : Rail}}

# set rail dimensions and insert cube to create balconyony rails
Rail --> 
	 s('1.1,'1,0.1) t(0,0,-0.1) center(x) primitiveCube() Wall(0)

BalconyFloor --> Wall(0)




# ----------------------------------------
# Wall
# ----------------------------------------

	
# for the wall asset, setting the texture scale params u and v 
# guarantees a texture mapping that nicely fits over the whole facade
getStyle(floorindex) = 
	case floorindex == 0 : 1
	case floorindex == 1 : 1
	else : 2

Wall(type) -->
	// dark bricks with dirt
	case type == 1 :
		color(wallColor)
		set(material.colormap, wall_tex)  
		set(material.dirtmap, dirt_tex)
		projectUV(0) projectUV(2)
	// bright bricks with dirt	
	case type == 2 :
		color(wallColor)
		set(material.colormap, wall2_tex)  
		set(material.dirtmap, dirt_tex)
		projectUV(0) projectUV(2)
	// dirt only	
	else :
		color(wallColor)
		set(material.dirtmap, dirt_tex)
		projectUV(2)


SolidWall -->
		s('1,'1,wall_inset) t(0,0,-wall_inset)
		primitiveCube()
		Wall(1)		  	
		report("edges", 1)
		