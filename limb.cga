
version "2015.0"

@Hidden
attr boneSize = 0.2

@Handle(type=angular, shape=HandleA, axis=z, translate={0,0,0}, occlusion=false, color="#ffff00")
@Range(-180,180)
attr a = 0
@Handle(type=angular, shape=HandleB, axis=x, reference=origin, occlusion=false, color="#00ffff")
@Range(-90,90)
attr b = 0
@Handle(type=angular, shape=HandleC,translate={0,0.0,0}, axis=x, reference=origin, occlusion=false)
@Range(-120,120)
attr c = 0

@Hidden @Color
attr color = "#888888"

@Hidden
attr showLimbHandles = false

Leg -->  Arm

Arm --> 

	t ( boneSize/2, boneSize/2, boneSize/2)
	[r(0,0,90)	s (0.1, 0.1, 0.1) MaybeHandleA ]
	r (0, 0, -a)
	[r(0,0,0)	s (0.1, 0.1, 0.1) MaybeHandleB ]
	r (b, 0, 0)
	t ( -boneSize/2, -boneSize/2, -boneSize/2)
	
	s (boneSize, 0.5 * scope.sy + 0.5*boneSize, boneSize)
	[ Cylinder ]
	t (0, scope.sy-boneSize, 0)
	
	t ( 0, boneSize/2, boneSize/2)
	[s (0.1, 0.1, 0.1) MaybeHandleC]
	r (c, 0, 0)
	t ( 0, -boneSize/2, -boneSize/2)
	
	[ Cylinder ]
	
Lot --> 
	extrude (2)
	Arm
		
Lot2 --> extrude(2) X
		
Cylinder  -->
	split(y) {
		boneSize/2 : r(180,0,0) t('0, '-1, '-1) i ("hemisphere.obj") X |
		~1 :	     i ("cylinder.obj")  X |
		boneSize/2 : i ("hemisphere.obj") X 
	}

MaybeHandleA -->
	case showLimbHandles: HandleA
	else: NIL
HandleA --> NIL

MaybeHandleB -->
	case showLimbHandles: HandleB
	else: NIL
HandleB --> NIL

MaybeHandleC -->
	case showLimbHandles: HandleC
	else: NIL
HandleC --> NIL

X --> color (color) 
		