/**
 * File:    instanceCity_03.cga
 * Created: 5 May 2010 09:21:09 GMT
 * Author:  andi
 */

version "2014.0"
 
 
import instanceReporting: "instanceReporting_dist.cga" 
 

# select the asset that fits best 
attr scifiasset = assetBestSize("*assets/scifi/scifi_building_*.obj","xz")

@StartRule 
Lot -->  
	Ground 
	convexify comp(f){all : innerRect Footprint}

@StartRule 
LotCorner --> Ground
@StartRule 
LotInner --> Lot
  
  
  
Footprint --> 
	alignScopeToAxes(y)
	set(material.shininess, 20)
	set(material.specular.r, 0.7)
	set(material.specular.g, 0.7)
	set(material.specular.b, 0.7)
	Building(scifiasset)
	
	

 
Building(asset) --> 
	s('1,0,'1) 
	i(asset) Asset.
	instanceReporting.InstanceReport(asset)



###################################
# Streets
#
attr doBridges = true


@StartRule 
Street --> StreetTex Bridges 
@StartRule 
Sidewalk --> comp(f) {all: split(y) { 2 : StreetLine | ~1: Ground } } 
@StartRule 
Crossing --> StreetTex
@StartRule 
Junction --> StreetTex
@StartRule 
JunctionEntry --> StreetTex

StreetTex --> setupProjection(0.0, scope.xz, 600, 300)  texture("scifi/scifi_texture_street_dark.jpg") projectUV(0.0)
StreetLine --> setupProjection(0.0, world.xz, 900, 150) texture("scifi/scifi_texture_street_bright.jpg") projectUV(0.0) 
Ground --> setupProjection(0.0, world.xz, 300, 2048) projectUV(0.0) texture("scifi/scifi_texture_street_dark.jpg") 

@StartRule
Joint                         --> Street
@StartRule
Freeway                       --> Street
@StartRule
FreewayEntry                  --> Street
@StartRule
Roundabout                    --> Street
@StartRule
RoundaboutIsland              --> Sidewalk
@StartRule
RoundaboutEntry               --> Street


Bridges -->
	case doBridges :	
	split(x){~20 : Bridge}*
	else : NIL

calcHeight = (split.total/2-abs(split.total/2-split.index))/split.total*2
calcHeight2(size) = size+(size*calcHeight*calcHeight)*2

const bridge_asset = "scifi/bridge/bridge3.obj"

Bridge --> 
	s(1,8,scope.sz+3.2) center(xz) 
	i(bridge_asset) 
	s(calcHeight2(scope.sx),calcHeight2(scope.sy),'1)
	Bridge.
	instanceReporting.InstanceReport(bridge_asset)



	